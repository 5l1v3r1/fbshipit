<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

/**
 * This file was moved from fbsource to www. View old history in diffusion:
 * https://fburl.com/f97s02wi
 */
namespace Facebook\ShipIt\Config;

use namespace HH\Lib\Str;
use type Facebook\ShipIt\{
  FBShipItCLIStaticConfig,
  FBShipItConfig,
  FBSourceBranchConfig,
  GetPathMappingsFn,
  ShipItBaseConfig,
  ShipItChangeset,
  ShipItPathFilters,
};

final class FacebookFbshipit extends FBWWWBase {
  <<__Override>>
  public static function getStaticConfig(): FBShipItCLIStaticConfig {
    return shape(
      'internalRepo' => 'www',
      'githubOrg' => 'facebook',
      'githubProject' => 'fbshipit',
    );
  }

  <<__Override>>
  protected static function defaultProjectFilterChangeset(
    ShipItChangeset $changeset,
    GetPathMappingsFn $get_path_mappings,
  ): ShipItChangeset {
    return $changeset
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> Str\replace($path, '/public_tld/dot_', '/public_tld/.')
          |> \Regex::replace('@(github/.+)\.github@', '$1', $$),
      )
      |> parent::defaultProjectFilterChangeset($$, $get_path_mappings)
      |> self::changeExampleExtensions($$);
  }

  <<__Override>>
  protected static function getStripCommonFilesExceptions(): ImmVector<string> {
    return ImmVector {
      '@^flib/intern/opensource/shipit/config/.*$@',
    };
  }

  <<__Override>>
  protected static function getDefaultStrippedFiles(): ImmVector<string> {
    return ImmVector {
      '@/fb-private/@',
      '@(^|/)fb_.+\.sh$@',
      '@(^|/)fb_.+\.py$@',
      // Do not sync examples for old-style projects
      '@(^|/)scripts/opensource/shipit/(shipit_|importit_).*\.php@',
      '@(^|/)__tests__/(shipit|importit)/.*/projects/.*\.php@',
    };
  }

  <<__Override>>
  public static function getDefaultPathMappings(): ImmMap<string, string> {
    return ImmMap {
      'flib/intern/opensource/shipit/public_tld/' => '',
      'scripts/opensource/shipit/' => 'fb-examples/bin/',
      'flib/intern/opensource/shipit/config/fb-specific-but-public/' =>
        'fb-examples/config/',
      'flib/intern/opensource/shipit/src/config/fb-specific-but-public/' =>
        'fb-examples/lib/config/',
      'flib/intern/opensource/shipit/src/importit/fb-specific-but-public/' =>
        'fb-examples/lib/importit/',
      'flib/intern/opensource/shipit/src/shipit/fb-specific-but-public/' =>
        'fb-examples/lib/shipit/',
      'flib/intern/opensource/shipit/__tests__/importit/fb-specific-but-public/' =>
        'fb-examples/tests/importit/',
      'flib/intern/opensource/shipit/__tests__/shipit/fb-specific-but-public/' =>
        'fb-examples/tests/shipit/',
      'flib/intern/opensource/shipit/__tests__/config/fb-specific-but-public/' =>
        'fb-examples/tests/config/',
      'flib/intern/opensource/shipit/src/' => 'src/',
      'flib/intern/opensource/shipit/__tests__/' => 'tests/',
    };
  }

  <<__Override>>
  protected static function projectImportChangeset(
    ShipItChangeset $changeset,
    FBSourceBranchConfig $branch_config,
  ): ShipItChangeset {
    return $changeset
      |> self::reverseExampleExtensions($$)
      |> parent::projectImportChangeset($$, $branch_config)
      // Undo FBWWWBase assumptions about test locations
      // ShipIt tests are currently all in a root __tests__ above src/.
      |> ShipItPathFilters::rewritePaths(
        $$,
        /* HH_IGNORE_ERROR[2049] __PHPStdLib */
        /* HH_IGNORE_ERROR[4107] __PHPStdLib */
        $path ==> \preg_replace(
          '@src/(.+/)?(__tests__)/(.+)@',
          '\2/\1\3',
          $path,
        ),
      )
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> Str\replace($path, '/public_tld/.', '/public_tld/dot_')
          |> \Regex::replace('@(github/.+)@', '$1.github', $$),
      );
  }

  /**
   * Rename fb-examples/foo.php-example to fb-examples/foo.php.
   *
   * Inverse of changeExampleExtensions.
   */
  private static function reverseExampleExtensions(
    ShipItChangeset $changeset,
  ): ShipItChangeset {
    return ShipItPathFilters::rewritePaths(
      $changeset,
      /* HH_IGNORE_ERROR[2049] __PHPStdLib */
      /* HH_IGNORE_ERROR[4107] __PHPStdLib */
      $path ==> \preg_replace('@^(fb-examples/.+\.php)-example$@', '\1', $path),
    );
  }

  /**
   * Rename fb-examples/foo.php to fb-examples/foo.php-example.
   *
   * These files depend on some private code (eg fetching GitHub API tokens) -
   * so if these are .php files, the typechecker will fail.
   */
  private static function changeExampleExtensions(
    ShipItChangeset $changeset,
  ): ShipItChangeset {
    return ShipItPathFilters::rewritePaths(
      $changeset,
      /* HH_IGNORE_ERROR[2049] __PHPStdLib */
      /* HH_IGNORE_ERROR[4107] __PHPStdLib */
      $path ==> \preg_replace('@^fb-examples/.+\.php$@', '\0-example', $path),
    );
  }
}
