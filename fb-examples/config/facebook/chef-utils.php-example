<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

/**
 * This file was moved from fbsource to www. View old history in diffusion:
 * https://fburl.com/w34dxay9
 */
namespace Facebook\ShipIt\Config;

use namespace HH\Lib\Str;
use type Facebook\ShipIt\{
  FBShipItCLIStaticConfig,
  FBShipItConfig,
  FBSourceBranchConfig,
  GetPathMappingsFn,
  OSTeamFilters,
  ShipItBaseConfig,
  ShipItChangeset,
};

final class FacebookChefUtils extends FBShipItConfig {
  public static function getDirs(): ImmSet<string> {
    return ImmSet {
      'chef-utils-github/',
      'scripts/chef/',
    };
  }

  public static function stripChefTestHostsHeader(
    ShipItChangeset $changeset,
  ): ShipItChangeset {
    return $changeset->withMessage(
      /* HH_IGNORE_ERROR[2049] __PHPStdLib */
      /* HH_IGNORE_ERROR[4107] __PHPStdLib */
      \preg_replace(
        '/\s*chef_test_hosts:.*/',
        '',
        $changeset->getMessage(),
      ),
    );
  }

  <<__Override>>
  protected static function defaultProjectFilterChangeset(
    ShipItChangeset $changeset,
    GetPathMappingsFn $get_path_mappings,
  ): ShipItChangeset {
    return parent::defaultProjectFilterChangeset($changeset, $get_path_mappings)
      |> OSTeamFilters::rewriteEmails($$)
      |> static::stripChefTestHostsHeader($$);
  }

  <<__Override>>
  protected static function getDefaultStrippedFiles(): ImmVector<string> {
    # Unlike most shipit plugins we effectively whitelist because we can't
    # easily move chefctl.rb (yet)
    #
    # Therefore, below we do a NegativeLookAhead regex and anything that's not
    # in this list gets rejected.
    #
    # For scripts/chefctl-github/ the entire directory is included, so that all
    # works normally. But for scripts/chef/ we only include a single file, so
    # for that directory we ALSO have to explicitly allow the shipit test file
    # or it will crash.
    $valid = vec[
      'chef/chefctl.rb',
      'chef/shipit_test_file.txt',
    ];
    return ImmVector {
      '@^scripts\/(?!('.Str\join($valid, '|').'))@',
    };
  }

  <<__Override>>
  public static function getDefaultPathMappings(): ImmMap<string, string> {
    return ImmMap {
      # order is important - most specific first
      'chef-utils-github/' => '',
      'scripts/chef/' => 'chefctl/src/',
    };
  }

  <<__Override>>
  public static function getStaticConfig(): FBShipItCLIStaticConfig {
    return shape(
      'internalRepo' => 'opsfiles',
      'githubOrg' => 'facebook',
      'githubProject' => 'chef-utils',
    );
  }

  <<__Override>>
  public static function getBaseConfig(
    FBSourceBranchConfig $branch_config,
  ): ShipItBaseConfig {
    return parent::getBaseConfig($branch_config)
      ->withSourceRoots(self::getDirs());
  }
}
