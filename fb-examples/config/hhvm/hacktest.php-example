<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

/**
 * This file was moved from fbsource to www. View old history in diffusion:
 * https://fburl.com/vwzc8pra
 */
namespace Facebook\ShipIt\Config;

use namespace HH\Lib\Str;
use type Facebook\ShipIt\{
  FBSourceBranchConfig,
  FBShipItCLIStaticConfig,
  GetPathMappingsFn,
  ShipItChangeset,
  ShipItPathFilters,
};

final class HhvmHacktest extends FBWWWBase {
  const string ROOT = 'flib/intern/testinfra/hacktest/opensource/';

  <<__Override>>
  public static function getDefaultPathMappings(): ImmMap<string, string> {
    return ImmMap {
      static::ROOT.'__github__/tests/' => 'tests/',
      static::ROOT.'__github__/src/HackTestCLI_dot_php' =>
        'src/HackTestCLI.php',
      static::ROOT.'__github__/src/Retriever/' => 'src/Retriever/',
      static::ROOT.'__github__/src/Runner/' => 'src/Runner/',
      static::ROOT.'__github__/dot_' => '.',
      static::ROOT.'__github__/' => '',
      static::ROOT => 'src/',
    };
  }

  <<__Override>>
  final protected static function defaultProjectFilterChangeset(
    ShipItChangeset $changeset,
    GetPathMappingsFn $get_path_mappings,
  ): ShipItChangeset {
    return parent::defaultProjectFilterChangeset($changeset, $get_path_mappings)
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> Str\replace($path, '_dot_php', '.php'),
      );
  }

  <<__Override>>
  final protected static function projectImportChangeset(
    ShipItChangeset $changeset,
    FBSourceBranchConfig $branch_config,
  ): ShipItChangeset {
    // This is pretty ugly as *in general*, www projects should
    // map tests/foo/bar.php to foo/__tests__/bar.php ...
    // but for hacktest, currently all tests are open source-only
    return $changeset
      |> ShipItPathFilters::rewritePaths(
        $$,
        /* HH_IGNORE_ERROR[2049] __PHPStdLib */
        /* HH_IGNORE_ERROR[4107] __PHPStdLib */
        $path ==> \preg_replace('@^tests/@', '__github__/tests/', $path),
      )
      |> ShipItPathFilters::rewritePaths(
        $$,
        /* HH_IGNORE_ERROR[2049] __PHPStdLib */
        /* HH_IGNORE_ERROR[4107] __PHPStdLib */
        $path ==> \preg_replace('/^\\./', 'dot_', $path),
      )
      |> ShipItPathFilters::rewritePaths(
        $$,
        /* HH_IGNORE_ERROR[2049] __PHPStdLib */
        /* HH_IGNORE_ERROR[4107] __PHPStdLib */
        $path ==> \preg_replace(
          '@^((src/(Retriever|Runner)|__github__/tests)/.+)\.php$@',
          '\1_dot_php',
          $path,
        ),
      )
      |> parent::projectImportChangeset($$, $branch_config)
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> Str\replace($path, '__github__/__github__', '__github__'),
      );
  }

  <<__Override>>
  public static function getStaticConfig(): FBShipItCLIStaticConfig {
    return shape(
      'internalRepo' => 'www',
      'githubOrg' => 'hhvm',
      'githubProject' => 'hacktest',
    );
  }
}
