<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

/**
 * This file was moved from fbsource to www. View old history in diffusion:
 * https://fburl.com/ws7v1qac
 */
namespace Facebook\ImportIt;

use namespace HH\Lib\Str;


use type Facebook\ShipIt\{ShipItChangeset};

// TODO(T22473111) delete this class after Buck is migrated
abstract class FBProjectBaseTest extends \Facebook\ShipIt\BaseTest {
  abstract const type TImportIt as FBImportItCLI;
  abstract const type TShipItTest as \Facebook\ShipIt\FBProjectBaseTest;

  /**
   * Example input/output paths for filter.
   *
   * Projects can override this, but by default, it uses the inverse of the
   * ShipIt test.
   *
   * @returns a map from source path to either a destination path, or null if
   *   changes to the specified internal path should be removed from the
   *   destination repository.
   */
  protected static function getExamplePathMappings(): ImmMap<string, string> {
    $shipit_test_class = static::getShipItTestClass();
    $shipit_examples = $shipit_test_class::getExamplePathMappings();
    $mapping = Map {};
    foreach ($shipit_examples as $dest_path => $src_path) {
      if ($src_path === null) {
        continue;
      }
      $mapping[$src_path] = $dest_path;
    }
    return $mapping->toImmMap();
  }

  protected static function map(
    \Facebook\ShipIt\ShipItChangeset $changeset,
  ): \Facebook\ShipIt\ShipItChangeset {
    $importit_class = static::getImportItClass();
    return $importit_class::filterChangeset($changeset);
  }

  final public function examplePathMappings(): dict<string, (string, ?string)> {
    return static::getExamplePathMappings()
      ->mapWithKey(($k, $v) ==> tuple($k, $v))
      |> dict($$);
  }

  <<\DataProvider('examplePathMappings')>>
  public function testPathMapping(
    string $path_in,
    ?string $path_expected,
  ): void {
    $diffs = ImmVector {shape('path' => $path_in, 'body' => 'junk')};
    $changeset = (new ShipItChangeset())->withDiffs($diffs);
    $this->pathMappingCheck($changeset, $path_in, $path_expected);
  }

  final protected function pathMappingCheck(
    ShipItChangeset $changeset,
    string $path_in,
    ?string $path_expected,
  ): void {
    $changeset = static::map($changeset);

    if ($path_expected === null) {
      $expected_paths = vec[];
      $message = Str\format('Expected `%s` to be stripped', $path_in);
    } else {
      $expected_paths = vec[$path_expected];
      $message = Str\format(
        'Expected `%s` to map to `%s`',
        $path_in,
        $path_expected,
      );
    }
    \expect(vec($changeset->getDiffs()->map($diff ==> $diff['path'])))
      ->toBePHPEqual(
        $expected_paths,
        Str\format(
          "%s.  Debug messages follow:\n%s",
          $message,
          Str\join($changeset->getDebugMessages(), "\n"),
        ),
      );
  }

  final public function testBranches(): void {
    $importit_class = static::getImportItClass();
    $shipit_class = static::getShipItClass();
    $importit_base_config = $importit_class::getBaseConfig();
    $shipit_base_config = $shipit_class::getBaseConfig();
    \expect($importit_base_config->getDestinationBranch())->toBePHPEqual(
      $shipit_base_config->getSourceBranch(),
      'Destination branch should be the inverse of the source branch.',
    );
    \expect($importit_base_config->getSourceBranch())->toBePHPEqual(
      $shipit_base_config->getDestinationBranch(),
      'Source branch should be the inverse of the destination branch.',
    );
  }

  final public function testRepositoryNameCollision(): void {
    $importit_class = self::getImportItClass();
    $config = $importit_class::getBaseConfig();
    \expect($config->getDestinationPath())->toNotBePHPEqual(
      $config->getSourcePath(),
      'ShipIt repo names match and `getStaticConfig` has not set '.
      '`destDirName` to something unique for the destination repository!',
    );
  }

  final protected static function getImportItClass(): classname<FBImportItCLI> {
    $ts = type_structure(static::class, 'TImportIt');
    invariant($ts['kind'] === TypeStructureKind::OF_CLASS, 'unexpected type');
    return $ts['classname'];
  }

  final protected static function getShipItClass(
  ): classname<ISupportsFBImportIt> {
    // UNSAFE_EXPR
    return self::invoke_static_bypass_visibility(
      static::getImportItClass(),
      'getShipItClass',
    );
  }

  final protected static function getShipItTestClass(
  ): classname<\Facebook\ShipIt\FBProjectBaseTest> {
    $ts = type_structure(static::class, 'TShipItTest');
    invariant($ts['kind'] === TypeStructureKind::OF_CLASS, 'unexpected type');
    return $ts['classname'];
  }
}
