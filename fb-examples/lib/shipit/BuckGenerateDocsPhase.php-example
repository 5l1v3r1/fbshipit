<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

/**
 * This file was moved from fbsource to www. View old history in diffusion:
 * https://fburl.com/pvccftga
 */
namespace Facebook\ShipIt;

use namespace HH\Lib\Str;

/**
 * Generate buck documentation from source repo into the destination repo.
 */
final class BuckGenerateDocsPhase extends ShipItPhase {
  const GH_PAGES_BRANCH = 'gh-pages';

  <<__Override>>
  public function isProjectSpecific(): bool {
    return true;
  }

  <<__Override>>
  public function getReadableName(): string {
    return 'Generate buck documentation';
  }

  <<__Override>>
  protected function runImpl(ShipItBaseConfig $config): void {
    $sourcePath = $config->getSourcePath();
    $docsPath = $config->getDestinationPath();
    $branch = $config->getDestinationBranch();

    $verbose = $config->isVerboseEnabled()
      ? ShipItUtil::VERBOSE_SHELL_OUTPUT
      : ShipItUtil::DONT_VERBOSE;

    // Get out of our current branch so it can be deleted.
    /* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $docsPath,
      /* stdin = */ null,
      $verbose,
      'git', 'checkout', '--detach',
    );

    // Delete the current branch.
    /* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $docsPath,
      /* stdin = */ null,
      $verbose,
      'git', 'branch', '--delete', '--force', $branch,
    );

    // Create a historyless branch in its place.
    /* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $docsPath,
      /* stdin = */ null,
      $verbose,
      'git', 'checkout', '--orphan', $branch,
    );

    // Erase all existing files.
    /* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $docsPath,
      /* stdin = */ null,
      $verbose,
      'git', 'rm', '--force', '-r', '.',
    );

    $pipes = varray[];
    /* HH_IGNORE_ERROR[2049] __PHPStdLib */
    /* HH_IGNORE_ERROR[4107] __PHPStdLib */
    $proc = \proc_open(
      'docs/soyweb-prod.sh',
      darray[
        0 => varray['pipe', 'r'],
        1 => varray['file', '/dev/null', 'w'],
        2 => varray['file', '/dev/null', 'w'],
      ],
      &$pipes,
      $sourcePath
    );
    /* HH_IGNORE_ERROR[2049] __PHPStdLib */
    /* HH_IGNORE_ERROR[4107] __PHPStdLib */
    \fclose($pipes[0]);

    try {
      /* HH_FIXME[4128] Use ShipItShellCommand */
      ShipItUtil::shellExec(
        $sourcePath,
        /* stdin = */ null,
        $verbose,
        'docs/soy2html.sh', $docsPath,
      );
    } finally {
      /* HH_IGNORE_ERROR[2049] __PHPStdLib */
      /* HH_IGNORE_ERROR[4107] __PHPStdLib */
      $status = \proc_get_status($proc);
      if ($status !== null) {
        // kill whole tree of docs/soyweb-prod.sh
        /* HH_IGNORE_ERROR[2049] __PHPStdLib */
        /* HH_IGNORE_ERROR[4107] __PHPStdLib */
        \exec('pstree -pl '.$status['pid'].
             ' | grep -o "([0-9]\+)" | sed "s/(\|)//g" | xargs kill');
      }
    }

    /* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $docsPath,
      /* stdin = */ null,
      $verbose,
      'git', 'add', '.',
    );

    /* HH_FIXME[4128] Use ShipItShellCommand */
    $sourceCommitHash = Str\trim_right(ShipItUtil::shellExec(
      $sourcePath,
      /* stdin = */ null,
      $verbose,
      'git', 'log', '-1', '--format=%H',
    ));
    /* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $docsPath,
      /* stdin = */ null,
      $verbose,
      'git', 'commit',
      '-m', Str\format('Updated HTML documentation to %s.', $sourceCommitHash),
    );
  }
}
